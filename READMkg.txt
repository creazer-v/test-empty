locals {
  sec_group_tag = {
    "Name" = "DataPlatform-SecurityGroup"
  }
}

resource "aws_cloudwatch_log_group" "rds-audit" {
  count             = var.instance_count
  name              = "/aws/rds/instance/${var.instance_count > 1 || var.read_replica == true ? replace(local.dbi, "-${var.identifier}", "-0${count.index + 1}") : local.dbi}/audit"
  retention_in_days = var.retention_in_days_rds_audit
  kms_key_id        = var.kms_key_id_log_group
  skip_destroy      = false
  tags              = merge(var.tags, local.tags_db)
}

resource "aws_cloudwatch_log_group" "rds-alert" {
  count             = var.instance_count
  name              = "/aws/rds/instance/${var.instance_count > 1 || var.read_replica ? replace(local.dbi, "-${var.identifier}", "-0${count.index + 1}") : local.dbi}/alert"
  retention_in_days = var.retention_in_days_rds_alert
  kms_key_id        = var.kms_key_id_log_group
  skip_destroy      = false
  tags              = merge(var.tags, local.tags_db)
}

resource "aws_cloudwatch_log_group" "rds-listener" {
  count             = var.instance_count
  name              = "/aws/rds/instance/${var.instance_count > 1 || var.read_replica ? replace(local.dbi, "-${var.identifier}", "-0${count.index + 1}") : local.dbi}/listener"
  retention_in_days = var.retention_in_days_rds_listener
  kms_key_id        = var.kms_key_id_log_group
  skip_destroy      = false
  tags              = merge(var.tags, local.tags_db)
}

resource "aws_cloudwatch_log_group" "rds-trace" {
  count             = var.instance_count
  name              = "/aws/rds/instance/${var.instance_count > 1 || var.read_replica ? replace(local.dbi, "-${var.identifier}", "-0${count.index + 1}") : local.dbi}/trace"
  retention_in_days = var.retention_in_days_rds_trace
  kms_key_id        = var.kms_key_id_log_group
  skip_destroy      = false
  tags              = merge(var.tags, local.tags_db)
}

resource "aws_db_instance" "oracle" {
  depends_on = [
    aws_cloudwatch_log_group.rds-audit,
    aws_cloudwatch_log_group.rds-alert,
    aws_cloudwatch_log_group.rds-listener,
    aws_cloudwatch_log_group.rds-trace
  ]
  count                                 = var.instance_count
  character_set_name                    = var.character_set_name
  identifier                            = var.instance_count > 1 || var.read_replica ? replace(local.dbi, "-${var.identifier}", "-0${count.index + 1}") : local.dbi
  final_snapshot_identifier             = var.skip_final_snapshot == false ? "Final-snapshot-${var.instance_count > 1 || var.read_replica ? replace(local.dbi, "-${var.identifier}", "-0${count.index + 1}") : local.dbi}" : var.final_snapshot_identifier
  engine                                = var.read_replica == true || var.cross_region == true ? null : var.engine
  engine_version                        = var.read_replica == true || var.cross_region == true ? null : var.engine_version
  instance_class                        = var.instance_class
  allocated_storage                     = var.read_replica ? null : var.allocated_storage
  max_allocated_storage                 = var.read_replica ? null : var.max_allocated_storage
  storage_type                          = var.storage_type
  storage_encrypted                     = true
  kms_key_id                            = var.kms_key_id
  license_model                         = var.license_model
  db_name                               = var.restore_to_point_in_time == null ? var.read_replica || var.cross_region ? null : upper(local.dbn) : null
  username                              = var.read_replica == true || var.cross_region == true ? null : "antmsysdba"
  password                              = var.read_replica == true || var.cross_region == true ? null : resource.random_password.password.result
  port                                  = var.port
  iam_database_authentication_enabled   = var.iam_database_authentication_enabled
  replicate_source_db                   = var.replicate_source_db
  snapshot_identifier                   = var.snapshot_identifier
  vpc_security_group_ids                = [aws_security_group.this.id]
  option_group_name                     = var.read_replica == true || var.cross_region == true ? null : aws_db_option_group.option_group[0].id
  parameter_group_name                  = aws_db_parameter_group.parameter_group.id
  db_subnet_group_name                  = var.read_replica == true ? null : aws_db_subnet_group.default[0].name
  availability_zone                     = var.availability_zone
  multi_az                              = var.multi_az
  iops                                  = var.storage_type == "io1" ? var.iops : null
  publicly_accessible                   = false
  allow_major_version_upgrade           = var.allow_major_version_upgrade
  auto_minor_version_upgrade            = var.auto_minor_version_upgrade
  apply_immediately                     = var.apply_immediately
  maintenance_window                    = var.maintenance_window
  skip_final_snapshot                   = var.skip_final_snapshot
  copy_tags_to_snapshot                 = var.copy_tags_to_snapshot
  backup_retention_period               = var.read_replica ? null : var.backup_retention_period
  backup_window                         = var.read_replica ? null : var.backup_window
  monitoring_interval                   = var.monitoring_interval
  monitoring_role_arn                   = var.monitoring_role_arn
  enabled_cloudwatch_logs_exports       = var.enabled_cloudwatch_logs_exports
  deletion_protection                   = var.deletion_protection
  performance_insights_enabled          = var.performance_insights_enabled
  performance_insights_kms_key_id       = var.performance_insights_kms_key_id
  performance_insights_retention_period = var.performance_insights_retention_period
  delete_automated_backups              = var.delete_automated_backups
  dynamic "restore_to_point_in_time" {
    for_each = var.restore_to_point_in_time != null ? var.restore_to_point_in_time : []
    content {
      restore_time                  = lookup(restore_to_point_in_time.value, "restore_time", null)
      source_db_instance_identifier = lookup(restore_to_point_in_time.value, "source_db_instance_identifier", null)
      source_dbi_resource_id        = lookup(restore_to_point_in_time.value, "source_dbi_resource_id", null)
      use_latest_restorable_time    = lookup(restore_to_point_in_time.value, "use_latest_restorable_time", null)
    }
  }

  timeouts {
    create = var.db_instance_creation_timeout
  }

  #  tags = merge(module.mandatory_tags.tags, var.tags, local.tags_db)
  tags = merge(var.tags, local.tags_db)
  lifecycle {
    ignore_changes = [allocated_storage, db_name, engine_version, instance_class, max_allocated_storage, username, password, tags["db-patch-time-window"], tags["db-patch-schedule"], tags["prepatch-snapshot-flag"], tags["created-by"]]
  }
}

resource "aws_db_instance_role_association" "oracle" {
  count                  = var.aws_db_instance_role_association ? 1 : 0
  db_instance_identifier = resource.aws_db_instance.oracle[0].identifier
  feature_name           = "S3_INTEGRATION"
  role_arn               = var.role_arn
}

resource "aws_db_instance_role_association" "efs_integration" {
  count                  = var.efs_integration_instance_role_association ? 1 : 0
  db_instance_identifier = resource.aws_db_instance.oracle[0].identifier
  feature_name           = "EFS_INTEGRATION"
  role_arn               = var.efs_integration_role_arn
}

locals {
  json = jsondecode(
    file(
      var.oracle_parameter_option_group
    ),
  )
}
resource "aws_db_parameter_group" "parameter_group" {
  name        = "antmdbpg-${local.dbi}"
  description = "Anthem DB Parameter Group for ${local.dbi}"
  family      = var.family
  dynamic "parameter" {
    for_each = local.json.parameter_group_parameters
    content {
      name         = parameter.value.name
      value        = parameter.value.value
      apply_method = lookup(parameter.value, "apply_method", null)
    }
  }

  #tags = merge(module.mandatory_tags.tags, var.tags, local.tags_db)
  tags = merge(var.tags, local.tags_db)
  lifecycle {
    ignore_changes = [family, tags["db-patch-time-window"], tags["db-patch-schedule"], tags["prepatch-snapshot-flag"]]
  }
}
resource "aws_db_option_group" "option_group" {
  count                    = var.read_replica == true || var.cross_region == true ? 0 : 1
  name                     = "antmdbog-${local.dbi}"
  option_group_description = "Anthem DB Option Group for ${local.dbi}"
  engine_name              = var.engine_name
  major_engine_version     = var.major_engine_version
  dynamic "option" {
    for_each = var.enable_ssl_option == false ? toset(local.json.option_group_options) : toset(local.json.ssl_option)
    content {
      option_name                    = option.value.option_name
      port                           = var.enable_ssl_option == false ? lookup(option.value, "port", "0") : lookup(option.value, "port", "2484")
      version                        = lookup(option.value, "version", null)
      db_security_group_memberships  = var.enable_ssl_option == false ? lookup(option.value, "db_security_group_memberships", []) : lookup(option.value, "db_security_group_memberships", [])
      vpc_security_group_memberships = var.enable_ssl_option == false ? lookup(option.value, "vpc_security_group_memberships", []) : var.ssl_option_security_group_memberships #lookup(option.value, "vpc_security_group_memberships", var.ssl_option_security_group_memberships)
      dynamic "option_settings" {
        for_each = lookup(option.value, "option_settings", "")
        content {
          name  = lookup(option_settings.value, "name", null)
          value = lookup(option_settings.value, "value", null)
        }
      }
    }
  }

  # tags = merge(module.mandatory_tags.tags, var.tags, local.tags_db)
  tags = merge(var.tags, local.tags_db)
  lifecycle {
    ignore_changes = [tags["db-patch-time-window"], tags["db-patch-schedule"], tags["prepatch-snapshot-flag"]]
  }
}
resource "aws_db_subnet_group" "default" {
  count       = var.read_replica ? 0 : 1
  name        = "antmdbsn-${local.dbi}"
  subnet_ids  = data.aws_subnets.subnets.ids
  description = "Anthem DB Subnet Group for ${local.dbi}"

  # tags = merge(module.mandatory_tags.tags, var.tags, local.tags_db)
  tags = merge(var.tags, local.tags_db)
  lifecycle {
    ignore_changes = [subnet_ids, tags["db-patch-time-window"], tags["db-patch-schedule"], tags["prepatch-snapshot-flag"]]
  }
}

data "aws_vpc" "vpc" {
   id = var.vpc_id
   filter {
     name   = "tag:Name"
     values = ["aws-landing-zone-VPC", "lz-additional-vpc-VPC"]
   }
 }

data "aws_subnets" "subnets" {
  filter {
    name   = "vpc-id"
    values = [var.vpc_id == null ? "${data.aws_vpc.vpc.id}" : var.vpc_id]

  }

  tags = {
    Network = "Private"
  }
}

data "aws_subnet" "selected" {
  for_each = toset(data.aws_subnets.subnets.ids)
  id = each.value
 }

locals {
  filtered_subnet_ids = [
    for subnet_id, subnet in data.aws_subnet.selected :
    subnet_id if subnet.available_ip_address_count > 5
  ]
}

resource "aws_security_group" "this" {
  name        = "antmdbsg-${local.dbi}"
  description = "Anthem DB Security Group for ${local.dbi}"
  vpc_id      = var.vpc_id

  # tags = merge(module.mandatory_tags.tags, var.tags, local.sec_group_tag)
  tags = merge(var.tags, local.tags_db, local.sec_group_tag)
  lifecycle {
    ignore_changes = [tags["db-patch-time-window"], tags["db-patch-schedule"], tags["prepatch-snapshot-flag"]]
  }
}

resource "aws_security_group_rule" "anthem" {
  count                    = length(var.ingress_rules)
  type                     = "ingress"
  from_port                = lookup(var.ingress_rules[count.index], "from_port", 1521)
  to_port                  = lookup(var.ingress_rules[count.index], "to_port", 1521)
  protocol                 = lookup(var.ingress_rules[count.index], "protocol", "tcp")
  cidr_blocks              = lookup(var.ingress_rules[count.index], "cidr_blocks", null)
  description              = lookup(var.ingress_rules[count.index], "description", null)
  source_security_group_id = lookup(var.ingress_rules[count.index], "source_security_group_id", null)
  self                     = lookup(var.ingress_rules[count.index], "self", null)
  ipv6_cidr_blocks         = lookup(var.ingress_rules[count.index], "ipv6_cidr_blocks", null)
  prefix_list_ids          = lookup(var.ingress_rules[count.index], "prefix_list_ids", null)
  security_group_id        = aws_security_group.this.id
}

resource "aws_security_group_rule" "anthem_egress" {
  type        = "egress"
  from_port   = "0"
  to_port     = "0"
  protocol    = "-1"
  cidr_blocks = ["0.0.0.0/0"]
  description = "Elevance Health CloudDBA Patch Automation"

  security_group_id = aws_security_group.this.id

}

resource "random_password" "password" {
  length           = var.length
  keepers          = var.keepers
  lower            = var.lower
  min_lower        = var.min_lower
  min_numeric      = var.min_numeric
  min_special      = var.min_special
  min_upper        = var.min_upper
  numeric          = var.numeric
  override_special = var.override_special
  special          = var.special
  upper            = var.upper

}

resource "vault_generic_secret" "example" {
  depends_on = [aws_db_instance.oracle]
  count      = var.read_replica == true || var.cross_region == true ? 0 : 1

  path                = var.environment == "production" ? "corp-antmdb/aws-orcl-prod/${element(concat("${aws_db_instance.oracle.*.address}", [""]), 0)}/master" : "corp-antmdb/aws-orcl-nonprod/${element(concat("${aws_db_instance.oracle.*.address}", [""]), 0)}/master"
  delete_all_versions = var.delete_all_versions
  data_json = jsonencode({
    username = "antmsysdba"
    password = resource.random_password.password.result
  })

}
